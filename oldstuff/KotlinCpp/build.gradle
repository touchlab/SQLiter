/*
 * Copyright 2010-2017 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import kotlin.text.Regex
import org.jetbrains.kotlin.PlatformInfo
import org.jetbrains.kotlin.konan.target.*
import org.jetbrains.kotlin.konan.util.*
import org.jetbrains.kotlin.konan.util.DependencyProcessor

buildscript {
  apply from: "gradle/kotlinGradlePlugin.gradle"
  repositories {
    maven { url kotlinCompilerRepo }
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url "https://cache-redirector.jetbrains.com/maven-central" }
    jcenter()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-native-utils:$kotlinVersion"
    classpath "org.jetbrains.kotlin:kotlin-native-shared:$konanVersion"
    classpath "org.jetbrains.kotlin:kotlin-native-build-tools:$konanVersion"
    classpath 'com.github.jengelman.gradle.plugins:shadow:5.1.0'
  }
}

convention.plugins.platformInfo = PlatformInfo

ext {
    if(!rootProject.hasProperty('konanDevPath')){
        konanPath = new File(buildDir, "kndist").path
    } else {
        konanPath = konanDevPath
    }
    distDir = file('dist')
    dependenciesDir = DependencyProcessor.defaultDependenciesRoot
    experimentalEnabled = project.hasProperty("org.jetbrains.kotlin.native.experimentalTargets")
    platformManager = new PlatformManager(DistributionKt.buildDistribution(konanPath),
            ext.experimentalEnabled)

  kotlinCompilerModule="org.jetbrains.kotlin:kotlin-compiler:${kotlinVersion}"
  kotlinStdLibModule="org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
  kotlinCommonStdlibModule="org.jetbrains.kotlin:kotlin-stdlib-common:${kotlinStdlibVersion}:sources"
  kotlinTestCommonModule="org.jetbrains.kotlin:kotlin-test-common:${kotlinStdlibVersion}:sources"
  kotlinTestAnnotationsCommonModule="org.jetbrains.kotlin:kotlin-test-annotations-common:${kotlinStdlibVersion}:sources"
  kotlinReflectModule="org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
  kotlinScriptRuntimeModule="org.jetbrains.kotlin:kotlin-script-runtime:${kotlinVersion}"
  kotlinNativeUtilsModule="org.jetbrains.kotlin:kotlin-native-utils:${kotlinVersion}"
  kotlinUtilKliMetadatabModule="org.jetbrains.kotlin:kotlin-util-klib-metadata:${kotlinVersion}"

//  konanVersionFull = CompilerVersionGeneratedKt.getCurrentCompilerVersion()
//  gradlePluginVersion = konanVersionFull
}

allprojects {
  buildscript {
    repositories {
      maven {
        url 'https://cache-redirector.jetbrains.com/jcenter'
      }
    }
  }
  if (path != ":dependencies") {
    evaluationDependsOn(":dependencies")
  }

  repositories {
    maven {
      url 'https://cache-redirector.jetbrains.com/maven-central'
    }
    maven {
      url kotlinStdlibRepo
    }
    maven {
      url kotlinCompilerRepo
    }
    maven {
      url "https://dl.bintray.com/kotlin/kotlin-dev"
    }
  }

    setupHostAndTarget()
    setupClang(project)
}

void setupHostAndTarget() {
  ext.hostName = platformManager.hostName
  ext.targetList = platformManager.filteredOutEnabledButNotSupported.collect { it.visibleName } as List
  ext.konanTargetList = platformManager.enabled as List
}

void setupClang(Project project) {

    project.convention.plugins.platformManager = project.rootProject.ext.platformManager
    project.convention.plugins.execClang = new org.jetbrains.kotlin.ExecClang(project)

    project.plugins.withType(NativeComponentPlugin) {
        project.model {
            if (isWindows()) {
                platforms {
                    host {
                        architecture 'x86_64'
                    }
                }

                components {
                    withType(NativeComponentSpec) {
                        targetPlatform 'host'
                    }
                }

                toolChains {
                    gcc(Gcc) {
                        path "$llvmDir/bin"
                    }
                }
            } else {

                toolChains {
                    clang(Clang) {
                        hostPlatform.clang.clangPaths.each {
                            path it
                        }

                        eachPlatform { // TODO: will not work when cross-compiling
                            [cCompiler, cppCompiler, linker].each {
                                it.withArguments { it.addAll(project.hostPlatform.clang.clangArgs) }
                            }

                        }
                    }
                }
            }
        }
    }
}

def stdlib = 'klib/common/stdlib'

task clean {
    //dependsOn subprojects.collect { it.tasks.matching { it.name == "clean" } }
    doLast {
        delete distDir
    }
}
