/*
 * Copyright 2010-2017 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import groovy.io.FileType
import org.jetbrains.kotlin.konan.target.*
import org.jetbrains.kotlin.konan.properties.*
import org.jetbrains.kotlin.konan.util.*

buildscript {
    apply from: "gradle/kotlinGradlePlugin.gradle"
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-native-shared:$konanVersion"
    }
}
import org.jetbrains.kotlin.konan.*

convention.plugins.platformInfo = new PlatformInfo()

ext {
    distDir = file('dist')
    dependenciesDir = DependencyProcessor.defaultDependenciesRoot
    platformManager = new PlatformManager(DistributionKt.buildDistribution("${konanDevPath}"))

    kotlinCompilerModule="org.jetbrains.kotlin:kotlin-compiler:${kotlinVersion}"
    kotlinStdLibModule="org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
    kotlinReflectModule="org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
    kotlinScriptRuntimeModule="org.jetbrains.kotlin:kotlin-script-runtime:${kotlinVersion}"

    konanVersionFull = KonanVersion.Companion.CURRENT
    gradlePluginVersion = konanVersionFull.toString()
}

allprojects {
    if (path != ":dependencies") {
        evaluationDependsOn(":dependencies")
    }

    repositories {
        mavenCentral()
        maven {
            url kotlinCompilerRepo
        }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-dev/' }
    }

    setupHostAndTarget()
    loadCommandLineProperties()
    loadLocalProperties()
    setupClang(project)
}

void setupHostAndTarget() {
    ext.hostName = platformManager.hostName
    ext.targetList = platformManager.enabled.collect { it.visibleName } as List
    ext.konanTargetList = platformManager.enabled as List
}

void setupClang(Project project) {

    project.convention.plugins.platformManager = project.rootProject.ext.platformManager
    project.convention.plugins.execClang = new org.jetbrains.kotlin.ExecClang(project)

    project.plugins.withType(NativeComponentPlugin) {
        project.model {
            if (isWindows()) {
                platforms {
                    host {
                        architecture 'x86_64'
                    }
                }

                components {
                    withType(NativeComponentSpec) {
                        targetPlatform 'host'
                    }
                }

                toolChains {
                    gcc(Gcc) {
                        path "$llvmDir/bin"
                    }
                }
            } else {

                toolChains {
                    clang(Clang) {
                        hostPlatform.clang.clangPaths.each {
                            path it
                        }

                        eachPlatform { // TODO: will not work when cross-compiling
                            [cCompiler, cppCompiler, linker].each {
                                it.withArguments { it.addAll(project.hostPlatform.clang.clangArgs) }
                            }

                        }
                    }
                }
            }
        }
    }
}

void loadLocalProperties() {
    if (new File("$project.rootDir/local.properties").exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream("$project.rootDir/local.properties"))
        props.each { prop -> project.ext.set(prop.key, prop.value) }
    }
}

void loadCommandLineProperties() {
    if (project.hasProperty("konanc_flags")) {
        throw new Error("Specify either -Ptest_flags or -Pbuild_flags.")
    }
    ext.globalBuildArgs = project.hasProperty("build_flags") ? ext.build_flags.split() : []
    ext.globalTestArgs = project.hasProperty("test_flags") ? ext.test_flags.split() : []
    ext.testTarget = project.hasProperty("test_target") ? ext.test_target : null
}

class PlatformInfo {
    boolean isMac() {
        return HostManager.hostIsMac
    }

    boolean isWindows() {
        return HostManager.hostIsMingw
    }

    boolean isLinux() {
        return HostManager.hostIsLinux
    }

    Throwable unsupportedPlatformException() {
        return new TargetSupportException()
    }
}


def stdlib = 'klib/common/stdlib'


task clean {
    //dependsOn subprojects.collect { it.tasks.matching { it.name == "clean" } }
    doLast {
        delete distDir
    }
}
