buildscript {
    repositories {
        jcenter()
        google()
        gradlePluginPortal()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$KOTLIN_VERSION"
    }
}

apply plugin: 'org.jetbrains.kotlin.multiplatform'

repositories {
//    mavenLocal()
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

group = GROUP
version = VERSION_NAME

kotlin {
    targets {
        
        fromPreset(presets.jvm, 'jvm')
        
//                fromPreset(presets.macosX64, 'nativeCommon')
        
        fromPreset(presets.macosX64, 'macos'){
            compilations.each {
                it.extraOpts("-linker-options", "-lsqlite3")
            }
            compilations.test {
                it.extraOpts("-native-library", "../KotlinCpp/bcdist/macos_x64/tlruntime.bc")
            }
        }

        fromPreset(presets.iosX64, 'iosX64'){
            compilations.each {
                it.extraOpts("-linker-options", "-lsqlite3")
            }
            compilations.test {
                it.extraOpts("-native-library", "../KotlinCpp/bcdist/ios_x64/tlruntime.bc")
            }
        }
        
        fromPreset(presets.iosArm64, 'iosArm64'){
            compilations.each {
                it.extraOpts("-linker-options", "-lsqlite3")
            }
            compilations.test {
                it.extraOpts("-native-library", "../KotlinCpp/bcdist/ios_arm64/tlruntime.bc")
            }
        }
        
        fromPreset(presets.iosArm32, 'iosArm32'){
            compilations.each {
                it.extraOpts("-linker-options", "-lsqlite3")
            }
            compilations.test {
                it.extraOpts("-native-library", "../KotlinCpp/bcdist/ios_arm32/tlruntime.bc")
            }
        }
        
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation "co.touchlab:stately:$STATELY_VERSION"
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
            }
        }
        commonTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }
        jvmMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib'
            }
        }
        jvmTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }

        nativeCommonMain { }
        nativeCommonTest { }

        appleMain {
            dependsOn nativeCommonMain
        }

        configure([iosX64Main, iosArm64Main, macosMain, iosArm32Main]) {
            dependsOn appleMain
        }

        configure([iosX64Test, iosArm64Test, macosTest, iosArm32Test]) {
            dependsOn nativeCommonTest
        }
    }
}

configurations {
    compileClasspath
}

void mergeCppOutput(String archFolder, String archZip){
    ant.zip(
            update: "true",
            destfile: "${project.buildDir}/classes/kotlin/$archFolder/main/sqliter.klib"
    ) {
        zipfileset(
                dir: "${project.projectDir}/../KotlinCpp/bcdist/$archZip",
                prefix: "targets/$archZip/native"
        ) {
            include(name: '**.bc')
        }
    }
}

task mergeCppAll(dependsOn:build) {
    doLast {
        mergeCppOutput("iosArm32", "ios_arm32")
        mergeCppOutput("iosArm64", "ios_arm64")
        mergeCppOutput("iosX64", "ios_x64")
        mergeCppOutput("macos", "macos_x64")
    }
}

/*
 * Copyright (C) 2018 Touchlab, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: "$rootDir/gradle/gradle-mvn-mpp-push.gradle"

//publishing {
//    publications.all {
//        // Rewrite all artifacts from using the project name to just 'runtime'.
//        artifactId = artifactId.replace(project.name, 'sqliter')
//    }
//}

tasks.findByName("publish").dependsOn("mergeCppAll")
tasks.findByName("publishToMavenLocal").dependsOn("mergeCppAll")